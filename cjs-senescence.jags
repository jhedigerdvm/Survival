
model {

#prior for recapture prob
p ~ dbeta(1, 1)
   

#priors
age.beta[1] <- 0
# site.beta[1] <- 0
eps.capyear[1] <- 0
# eps.id[1] <- 0

int ~ dnorm(0, 0.001)

for (u in 2:14){     #15 age classes but 14 survival periods                        
   age.beta[u] ~ dnorm(0,0.01)              # Priors for age-specific survival
}

for (u in 2:15){      #capyear
  eps.capyear[u] ~ dnorm(0, tau.capyear)
}

  sigma ~ dunif(0,10) #standard dev
  tau.capyear <- 1/(sigma*sigma) #precision = 1/variance

# for (u in 2:691){ #animal_id
#   eps.id[u] ~ dnorm(0, tau.id)
# }
#   tau.id <-1/(sigma*sigma)

# Likelihood 
for (i in 1:nind){
   # Define latent state at first capture, we know for sure the animal is alive
      z[i,f[i]] <- 1
      
      for (t in (f[i]+1):h[i]){ 
        # State process
            z[i,t] ~ dbern(mu1[i,t]) #toss of a coin whether individual is alive or not detected 
            mu1[i,t] <- phi[i,t-1] * z[i,t-1]  #t-1 because we are looking ahead to see if they survived from 1 to 2 based upon them being alive at 2
            logit(phi[i,t-1]) <- int + age.beta[ageclass[i,t-1]] + eps.capyear[capyear[t-1]] #+ eps.id[id[i]]
        # Observation process
            CH[i,t] ~ dbern(mu2[i,t])
            mu2[i,t] <- p * z[i,t]
      } #t
   } #i


#derived parameter
    for (j in 1:11){ #age class
      survival[j] <- exp(int+ age.beta[j] )/ (1 + exp(int+ age.beta[j]))
    }                     #delta method to convert from logit back to probability Powell et al. 2007
  
    for (j in 1:11){
      surv_diff[j] <- survival[6] - survival[j]
    }

}

